plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.yunbok'
version = '0.0.1-SNAPSHOT'
description = 'houseping'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

// QueryDSL Q클래스 생성 경로
def generated = 'src/main/generated'

sourceSets {
    main {
        java {
            srcDirs += generated
        }
    }
}

tasks.named('compileJava') {
    options.annotationProcessorGeneratedSourcesDirectory = file(generated)
}

clean {
    delete file(generated)
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webflux' // WebClient용
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa' // JPA

    // Database
    runtimeOnly 'com.h2database:h2' // H2 Database

    // Environment
    implementation 'me.paulschwarz:spring-dotenv:4.0.0'

    // Monitoring
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // Logging & Utils
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // Scheduling
    implementation 'org.springframework.boot:spring-boot-starter-quartz'

    // Test Dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}
